/* データコンバータ */

#packopt name "converter"

sdim input_data, 1024
input_data = {"霞改,三式水中探,0,三式水中探,0,三式爆,0
扶桑,12型,7,六三四,7,六三四,7,12型,7
大淀,三式水中探,0,三式水中探,0,三式水中探,0,三式爆,0
夕張,三式水中探,0,三式水中探,0,三式水中探,0,三式爆,0
鹿島,三式水中探,0,三式水中探,0,四式水,0,三式爆,0
龍驤,村田,7,村田,7,流星(六,7,彩雲,7"}
sdim output_data, 1024

screen 0, 300, 400
title "コンバータ"
pos 10, 10
	mesbox input_data, 280, 100, 5
pos 10, 10 + 100 + 10
	button "変換", *convert
pos 10, 10 + 100 + 10 + 24 + 10
	mesbox output_data, 280, 236, 4
stop

*convert
	gosub *read_data

	output_data = ""
	output_data += "#司令部レベル\n100\n"
	;メンバー
	output_data += "\n#メンバー(カンマ区切り)\n"
	notesel input_data
	members = notemax
	name_list = ""
	for k, 0, members
		noteget get, k
		split get, ",", temp
		flg = 0
		for i, 0, kammusu_list_size
			if(instr(kammusu_list(i, 0),,temp(0)) != -1){
				flg = 1
				if(k != 0) :name_list += "," :output_data += ","
				name_list += kammusu_list(i, 0)
				output_data += kammusu_list(i, 1)
				_break
			}
		next
		if(flg == 0){
			output_data = "" + (k + 1) + "隻目の名前が読み取れませんでした。\n"
			objprm 2, output_data
			stop
		}
	next
	output_data += "\n"
	;艤装
	output_data += "\n#装備(カンマ区切り。無い場合は-1とする)\n#" + name_list + "\n"
	sdim name_list2, 256, members
	for k, 0, members
		noteget get, k
		split get, ",", temp
		if(stat \ 2 == 0){
			output_data = "" + (k + 1) + "隻目の装備の数がおかしいです。\n"
			objprm 2, output_data
			stop
		}
		weapons = (stat - 1) / 2
		for j, 0, weapons
			flg = 0
			for i, 0, weapon_list_size
				if(instr(weapon_list(i, 0),,temp(1 + j * 2)) != -1){
					flg = 1
					if(j != 0) :name_list2(k) += "," :output_data += ","
					name_list2(k) += weapon_list(i, 0)
					output_data += weapon_list(i, 1)
					_break
				}
			next
			if(flg == 0){
				output_data = "" + (k + 1) + "隻目の装備が読み取れませんでした。\n"
				objprm 2, output_data
				stop
			}
		next
		output_data += "\n"
	next
	;状態
	output_data += "\n#状態(残り耐久・cond値・残り弾薬％・1〜4スロ目の残り艦載機数)\n"
	output_data += "#それぞれ、「-1」とすると自動的に最大耐久・49・100・最大艦載機数に初期化される\n"
	for k, 0, members
		output_data += "-1,-1,-1,-1,-1,-1,-1\n";
	next
	;改修/熟練度
	output_data += "\n#各装備の改修状態\n"
	output_data += "#主砲等は0〜10、艦載機は0〜7で分類している\n"
	for k, 0, members
		output_data += "#" + name_list2(k) + "\n"
		noteget get, k
		split get, ",", temp
		weapons = (stat - 1) / 2
		for j, 0, weapons
			if(j != 0) :output_data += ","
			output_data += temp(2 + j * 2)
		next
		output_data += "\n"
	next
	objprm 2, output_data
	stop

*read_data
	notesel text_buffer
	;艦娘データ
	noteload "kammusu.csv"
	kammusu_list_size = notemax - 1
	sdim kammusu_list, 64, kammusu_list_size, 2
	for k, 0, kammusu_list_size
		noteget get, k + 1
		split get, ",", temp
		kammusu_list(k, 0) = temp(1)
		kammusu_list(k, 1) = temp(0)
	next
	;艤装データ
	noteload "weapon.csv"
	weapon_list_size = notemax - 1
	sdim weapon_list, 64, weapon_list_size, 2
	for k, 0, weapon_list_size
		noteget get, k + 1
		split get, ",", temp
		weapon_list(k, 0) = temp(1)
		weapon_list(k, 1) = temp(0)
	next
return

/*
霞,三式水,0,三式水,0,三式爆,0
扶桑,12型,7,六三四,7,六三四,7,12型,7
大淀,三式水,0,三式水,0,三式水,0,三式爆,0
夕張,三式水,0,三式水,0,三式水,0,三式爆,0
鹿島,三式水,0,三式水,0,四式水,0,三式爆,0
龍驤,村田,7,村田,7,流星(六,7,彩雲,7
*/

/*
*/